seed(12345)

# read stratigraphic ranges
taxa = readTaxonData(file = "data/dinosaur_ranges.tsv")

# read fossil counts
k <- readCharacterDataDelimited(file = "data/dinosaur_fossil_counts.tsv", header = true)

# interval boundaries
timeline <- v(100, 145, 201) - 66

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

alpha <- 10

# specify FBDR model parameters
for(i in 1:(timeline.size()+1))
{

	mu[i] ~ dnExp(alpha)
	lambda[i] ~ dnExp(alpha)
	psi[i] ~ dnExp(alpha)
	
	div[i] := lambda[i] - mu[i]
	turnover[i] := mu[i]/lambda[i]

	moves.append( mvSlide(mu[i], delta = 0.0001) )
	moves.append( mvSlide(mu[i], delta = 0.001) )
	moves.append( mvSlide(mu[i], delta = 0.01) )

	moves.append( mvSlide(lambda[i], delta = 0.0001) )
	moves.append( mvSlide(lambda[i], delta = 0.001) )
	moves.append( mvSlide(lambda[i], delta = 0.01) )

	moves.append( mvSlide(psi[i], delta = 0.0001) )
	moves.append( mvSlide(psi[i], delta = 0.001) )
	moves.append( mvSlide(psi[i], delta = 0.01) )
}

rho <- 0

# model 1
bd ~ dnFBDRMatrix(taxa=taxa, lambda=lambda, mu=mu, psi=psi, rho=rho, timeline=timeline, k=k)

moves.append( mvMatrixElementScale(bd, lambda = 0.01, weight=taxa.size()) )
moves.append( mvMatrixElementScale(bd, lambda = 0.1, weight=taxa.size()) )
moves.append( mvMatrixElementScale(bd, lambda = 1, weight=taxa.size()) )

moves.append( mvMatrixElementSlide(bd, delta = 0.01, weight=taxa.size()) )
moves.append( mvMatrixElementSlide(bd, delta = 0.1, weight=taxa.size()) )
moves.append( mvMatrixElementSlide(bd, delta = 1, weight=taxa.size()) )

mymodel = model(bd)

# add monitors
monitors.append( mnScreen(lambda, mu, psi, printgen=10) )
monitors.append( mnModel(filename="output/FBDRMatrix.log",printgen=1) )

# run the analysis
mymcmc = mcmc(mymodel, moves, monitors, moveschedule="random")
mymcmc.run(100)

q()
